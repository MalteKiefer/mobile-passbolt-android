plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "kotlin-kapt"
    id "com.google.firebase.appdistribution"
}

android {
    defaultConfig {
        applicationId "com.passbolt.mobile.android"
        versionCode Integer.valueOf(System.getenv().get("GITLAB_BUILD_NUMBER") ?: projectVersionCode)
        versionName projectVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseConfig {
            //TODO
            storeFile file("certs/release.keystore")
            storePassword System.getenv().get("RELEASE_STORE_PASSWORD") ?: ""
            keyAlias System.getenv().get("RELEASE_KEY_ALIAS") ?: ""
            keyPassword System.getenv().get("RELEASE_KEY_PASSWORD") ?: ""
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            firebaseAppDistribution {
                appId "1:660923335137:android:08eec6c125f36ba9b092a0"
                serviceCredentialsFile findProperty("serviceKey") ?: ""
                groups "qa"
            }

        }
//        qa {
//            applicationIdSuffix ".qa"
//            debuggable false
//            zipAlignEnabled true
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
//            matchingFallbacks = ["release"]
//            //TODO signingConfig signingConfigs.qaConfig
//        }
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.releaseConfig
        }
    }

    buildFeatures {
        viewBinding true
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    // needs to implement all modules:
    // https://github.com/google/dagger/issues/2378
    implementation project(':mvp')
    implementation project(':startup')
    implementation project(':setup')
    implementation project(':passboltapi')
    implementation project(':ui')
    implementation project(':common')
    implementation project(':storage')
    implementation project(':navigation')
    implementation project(':mappers')
    implementation project(':qrscan')

    debugImplementation libs.quality.leakcanary

    implementation libs.androidx.core
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintLayout
    implementation libs.androidx.navigationFragment
    implementation libs.androidx.navigationUi

    implementation libs.coil.core
    implementation libs.coil.base

    /* DI */
    implementation libs.di.koin
    implementation libs.di.koinExt
}
