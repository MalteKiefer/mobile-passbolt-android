apply plugin: 'jacoco'

apply from: "${rootDir}/gradle/jacocoExcludes.gradle"

task jacocoModuleReport(type: JacocoReport, dependsOn: 'unitTest') {

    group = "Reporting"
    description = "Jacoco test coverage reports for gradle module."

    reports {
        csv.enabled = false
        xml.enabled = true
        html.enabled = true
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    final mainSrc = "${project.projectDir}/src/main/java"
    final kotlinFileTree = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/debug", excludes: jacocoExcludes)
    final javacFileTree = fileTree(dir: "${project.buildDir}/intermediates/javac/debug", excludes: jacocoExcludes)

    sourceDirectories.setFrom files([mainSrc])
    classDirectories.setFrom files([kotlinFileTree, javacFileTree])
    executionData.setFrom files(fileTree(dir: project.buildDir, includes: ["/jacoco/*.exec"]))
}
