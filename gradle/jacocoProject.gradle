apply plugin: 'jacoco'

apply from: "gradle/jacocoExcludes.gradle"

task jacocoProjectReport(type: JacocoReport) {

    // execute this task after submodule tasks.
    subprojects.each {
        if (it.name != "Gopenpgp") {
            dependsOn("${it.name}:jacocoModuleReport")
        }
    }

    group = "Reporting"
    description = "Jacoco test coverage reports for whole project."

    reports {
        csv.enabled = false
        xml.enabled = true
        html.enabled = true
    }

    def sourceTree = []
    def classTree = []
    def execList = []

    rootProject.subprojects.forEach { module ->
        sourceTree.add("${module.projectDir}/src/main/java")

        classTree.add(fileTree(dir: "${module.buildDir}/tmp/kotlin-classes/debug", excludes: jacocoExcludes))
        classTree.add(fileTree(dir: "${module.buildDir}/intermediates/javac/debug", excludes: jacocoExcludes))
        execList.add(fileTree(dir: module.buildDir, includes: ["/jacoco/*.exec"]))
    }

    sourceDirectories.setFrom files(sourceTree)
    classDirectories.setFrom files(classTree)
    executionData.setFrom files(execList)
}
